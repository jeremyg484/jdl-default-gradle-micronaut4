configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
    implementation "com.h2database:h2"
}

def profiles = "dev"
if (project.hasProperty("no-liquibase")) {
    profiles += ",no-liquibase"
}
if (project.hasProperty("tls")) {
    profiles += ",tls"
}

tasks.withType(JavaExec) {
    classpath += configurations.developmentOnly
    jvmArgs('-XX:TieredStopAtLevel=1', '-Dcom.sun.management.jmxremote')
    if (System.getProperty("micronaut.environments")) {
        jvmArgs += '-Dmicronaut.environments=' + profiles + ',' + System.getProperty("micronaut.environments")
    } else {
        jvmArgs += '-Dmicronaut.environments=' + profiles
    }
    if (gradle.startParameter.continuous) {
        systemProperties(
            'micronaut.io.watch.restart':'true',
            'micronaut.io.watch.enabled':'true',
            "micronaut.io.watch.paths":"src/main"
        )
    }
}

task webapp(type: NpmTask) {
    inputs.files("package-lock.json")
    inputs.files("build.gradle")
    inputs.dir("src/main/webapp/")

    def webpackDevFiles = fileTree("webpack//")
    webpackDevFiles.exclude("webpack.prod.js")
    inputs.files(webpackDevFiles)

    outputs.dir("build/resources/main/static/")

    dependsOn npmInstall
    args = ["run", "webapp:build"]
    environment = [APP_VERSION: project.version]
}

task webappBuildDev(type: NpmTask, dependsOn: 'npmInstall') {
    inputs.dir("src/main/webapp/")
    inputs.files(fileTree('src/main/webapp/'))
    outputs.dir("build/www/")
    outputs.file("build/www/app/main.bundle.js")
    args = ["run", "webapp:build"]
}

task copyIntoStatic (type: Copy) {
    from 'build/www/'
    into 'build/resources/main/static'
}

processResources {
    inputs.property('version', version)
    filesMatching("**/application.yml") {
        filter {
            it.replace("#project.version#", version)
        }
    }
}

processResources.dependsOn webapp

processResources.dependsOn webappBuildDev
copyIntoStatic.dependsOn processResources
buildLayers.dependsOn copyIntoStatic
jar.dependsOn copyIntoStatic
shadowJar.dependsOn copyIntoStatic
compileTestJava.dependsOn copyIntoStatic
assemble.dependsOn copyIntoStatic

npmInstall.onlyIf { shouldWebappRun() == true }

def shouldWebappRun() {
    project.hasProperty('webapp')
}

